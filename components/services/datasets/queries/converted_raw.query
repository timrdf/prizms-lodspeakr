prefix dcterms:    <http://purl.org/dc/terms/>
prefix void:       <http://rdfs.org/ns/void#>
prefix datafaqs:   <http://purl.org/twc/vocab/datafaqs#>
prefix conversion: <http://purl.org/twc/vocab/conversion/>
prefix prov:       <http://www.w3.org/ns/prov#>
prefix dcat:       <http://www.w3.org/ns/dcat#>

select distinct ?void ?id
where {
   [] a datafaqs:CKANDataset;
      dcat:distribution ?dist
   .
   ?dist a dcat:Distribution .
   ?void
       a void:Dataset, conversion:AbstractDataset;
       prov:wasDerivedFrom ?dist;
       dcterms:identifier ?id;
       void:subset [
          a conversion:VersionedDataset;
          void:subset ?layer
       ]
   .
   ?layer
      a conversion:LayerDataset
   .
   optional { 
      ?layer conversion:enhancement_identifier ?e 
   }
   filter(!bound(?e))
} order by ?void


